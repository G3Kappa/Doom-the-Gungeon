#include "Actors/Weapons/Projectiles/GreenLaser.txt"

Class RogueSpecialAmmo : Ammo 
{
	const MaxAmount = 8;
	Default
	{
		Inventory.MaxAmount MaxAmount;
	}
}

Class RogueSpecial : WeaponBase replaces Pistol
{
	Default 
	{
		Inventory.PickupMessage "Underhanded and efficient";
		Weapon.SlotNumber 2;
		Weapon.AmmoType "RogueSpecialAmmo";
		Weapon.AmmoGive RogueSpecialAmmo.MaxAmount;
		Weapon.AmmoUse 1;
	}
	
	States
	{
		Spawn:
			RSGN A -1;
			Stop;
			
		Ready:
			RSGN A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
			
		Select:
			RSGN A 1 A_Raise;
			Loop;
			
		Deselect:
			RSGN A 1 A_Lower;
			Loop;
		
		Reload:
			RSGN R 0 A_JumpIf(CountInv("RogueSpecialAmmo") == RogueSpecialAmmo.MaxAmount, "Ready");
			RSGN R 42 Offset(0, 48) A_PlaySound("RogueSpecial/Reload", 1);
			RSGN R 0 A_GiveInventory("RogueSpecialAmmo", RogueSpecialAmmo.MaxAmount);
			Goto Ready;
			
		Fire:
			TNT1 A 0 A_GunFlash;
			TNT1 A 0 A_PlaySound("RogueSpecial/Fire", 1);
			TNT1 A 0 A_FireProjectile("GreenLaser", frandom(-6, 6), 1, 14, 4, 0, frandom(-1.3, 1.3));
			RSGN B 1 Offset(5,40);
			RSGN B 1 Offset(15,48);
			RSGN B 2 Offset(20,44);
			RSGN C 1 Offset(18,40);
			RSGN C 1 Offset(12,37);
			RSGN D 1 Bright Offset(8,35);
			RSGN D 1 Bright Offset(4,32);
			RSGN D 1 Offset(0,32);
			RSGN A 0 A_JumpIf(CountInv("RogueSpecialAmmo") == 0, "Reload");
			Goto Ready;
			
		Flash:
			RSGN A 2 Bright Light("L_GreenLaser");
			RSGN B 2 Bright Light("L_GreenLaser");
			Goto LightDone;
	}
}