#include "Actors/Weapons/Projectiles/Rebar.txt"

Class RailgunAmmo : Ammo 
{
	const MaxAmount = 40;
	Default
	{
		Inventory.MaxAmount MaxAmount;
	}
}

// Tracks the railgun's charge state. When at MaxAmount, the trigger can be released and the gun fires.
Class _RailgunCharge : Inventory { const MaxAmount = 35; Default { Inventory.MaxAmount MaxAmount; } }

Class Railgun : WeaponBase replaces Shotgun
{
	Default 
	{
		Inventory.PickupMessage "Calibrating";
		Weapon.SlotNumber 3;
		Weapon.AmmoType "RailgunAmmo";
		Weapon.AmmoGive RailgunAmmo.MaxAmount;
		Weapon.AmmoUse 1;
		+WEAPON.NOALERT;
	}
	
	States
	{
		Spawn:
			RGRD A -1;
			Stop;
			
		Ready:
			RGRD A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			RGRD A 0 A_JumpIf(CountInv("_RailgunCharge") > _RailgunCharge.MaxAmount - 1, "Charged");
			RGRD A 0 TakeInventory("_RailgunCharge", 1, TIF_NOTAKEINFINITE);
			Loop;
			
		Select:
			RGRD A 1 A_Raise;
			Loop;
			
		Deselect:
			RGRD A 1 A_Lower;
			Loop;
		
		Reload:
			Goto Ready;
			
		Flash:
			Goto LightDone;
			
		Fire: // Gun is charging
			TNT1 A 0 GiveInventory("_RailgunCharge", 1);
			TNT1 A 0 A_JumpIf(CountInv("_RailgunCharge") > _RailgunCharge.MaxAmount - 1, "HoldingCharge");
			TNT1 A 0 A_JumpIf(CountInv("_RailGunCharge") > _RailgunCharge.MaxAmount / 6 * 5, "Charge5");
			TNT1 A 0 A_JumpIf(CountInv("_RailGunCharge") > _RailgunCharge.MaxAmount / 6 * 4, "Charge4");
			TNT1 A 0 A_JumpIf(CountInv("_RailGunCharge") > _RailgunCharge.MaxAmount / 6 * 3, "Charge3");
			TNT1 A 0 A_JumpIf(CountInv("_RailGunCharge") > _RailgunCharge.MaxAmount / 6 * 2, "Charge2");
			Goto Charge1;
		
		Charge1:
			RGCH A 1 A_Lower;
			Goto Ready;
		Charge2:
			RGCH B 1 A_Lower;
			Goto Ready;
		Charge3:
			RGCH C 1 A_Lower;
			Goto Ready;
		Charge4:
			RGCH D 1 A_Lower;
			Goto Ready;
		Charge5:
			RGCH E 1 A_Lower;
			Goto Ready;
		HoldingCharge: // Gun is fully charged but BT_ATTACK is still pressed
			RGCH G 1 Bright A_Lower;
			Goto Ready;
			
		Charged: // Gun is charged and is actually firing
			TNT1 A 0 A_PlaySound("Railgun/Fire", 1);
			TNT1 A 0 A_AlertMonsters;
			RGCH E 1 A_FireProjectile("Rebar", 0, 1, 14, 4, 0, 0);
			RGCH D 1 Offset(10, 38);
			RGCH C 1 Offset(15, 42);
			RGCH B 1 Offset(20, 40);
			RGCH A 1 Offset(12, 36);
			RGCH A 1 Offset(6, 32);
			RGRD A 0 TakeInventory("_RailgunCharge", _RailgunCharge.MaxAmount);
			Goto Ready;
			
	}
}